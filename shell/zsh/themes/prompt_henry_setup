#!/usr/bin/env zsh

FIRST_CHARACTER_OK="▶"
FIRST_CHARACTER_KO="▶"
DIRTY="✗"
NONE="✓"

prompt_henry_pwd() {
  local -r prompt_short_dir=$("$DOTLY_PATH/bin/dot" core short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_henry_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on${(e)git_info[prompt]}"
}

prompt_henry_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_henry_setup() {
  local prompt_henry_status='%(?:%F{green}‼%F{white}⎇ :%F{red}⁉%F{white}⎇ )'

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_henry_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' ' %F{cyan}%b%c %C%D'

  PS1="${prompt_henry_status} \$(prompt_henry_pwd)\$(prompt_henry_git)%f "
  RPS1=''
}

prompt_henry_setup "${@}"
